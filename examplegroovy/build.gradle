plugins {
	alias(libs.plugins.androidApplication)
	alias(libs.plugins.kotlinAndroid)
	id("ch.ubique.gradle.alpaka")
}

android {
	namespace 'com.example.examplegroovy'
	compileSdk 34

	defaultConfig {
		applicationId "com.example.examplegroovy"
		minSdk 26
		targetSdk 34
		versionCode 1
		versionName "1.0"

		alpakaUploadKey = "defaultConfig upload key"
	}

	flavorDimensions = ["default"]

	productFlavors {
		dev {
			dimension "default"
			applicationIdSuffix ".dev"

			if (launcherIconLabel != "dev") {
				throw new GradleException("Expected launcherIconLabel 'dev' but was $launcherIconLabel")
			}
		}
		tescht {
			dimension "default"
			applicationIdSuffix ".test"
			alpakaUploadKey = "test flavor upload key"
			launcherIconLabel = "test"

			if (launcherIconLabel != "test") {
				throw new GradleException("Expected launcherIconLabel 'test' but was $launcherIconLabel")
			}
		}
		prod {
			dimension "default"

			if (launcherIconLabel != null) {
				throw new GradleException("Expected launcherIconLabel null but was $launcherIconLabel")
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_17.toString()
	}
}

dependencies {
	implementation libs.core.ktx
	implementation libs.appcompat
	implementation libs.material
	implementation libs.activity
	implementation libs.androidx.constraintlayout
}

alpaka {
	labelAppIcons = true
	changelogCommitCount = 5
}